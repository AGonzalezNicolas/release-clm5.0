#! /bin/csh -f

if !(-d $CASEBUILD) mkdir $CASEBUILD
if !(-d $CASEBUILD/clmconf) mkdir $CASEBUILD/clmconf

#--------------------------------------------------------------------
# Invoke cclm configure - output will go in CASEBUILD/clmconf
#--------------------------------------------------------------------

setenv COMP "unknown"
if ($COMP_INTERFACE == 'MCT' ) setenv COMP mct
if ($COMP_INTERFACE == 'ESMF') setenv COMP esmf

cd $CASEBUILD/clmconf  

set config_opts=" "
if ($LND_GRID == "reg" && $GRID != "CLM_USRDAT" ) then
   set config_opts=" -sitespf_pt $GRID"
endif

$CODEROOT/lnd/clm/bld/configure  $config_opts \
    -comp_intf cpl_\$COMP $CLM_CONFIG_OPTS -usr_src \$CASEROOT/SourceMods/src.clm || exit -1 

#--------------------------------------------------------------------
# Create clm.buildnml.csh
#--------------------------------------------------------------------

# copy necessary files to $CASEROOT/Tools/Templates/
if ($NINST_LND > 1) then
   set inst_counter = 1
   set inst_string = ""
   while ($inst_counter <= $NINST_LND)
      set inst_string = $inst_counter
      if ($inst_counter <= 999) set inst_string = "0$inst_string"
      if ($inst_counter <=  99) set inst_string = "0$inst_string"
      if ($inst_counter <=   9) set inst_string = "0$inst_string"
      set inst_string = _${inst_string}
      if ( ! -f "$CASEROOT/user_nl_clm${inst_string}" ) then
         cp $CODEROOT/lnd/clm/bld/user_nl_clm $CASEROOT/user_nl_clm${inst_string}
      endif
      @ inst_counter = $inst_counter + 1
   end 
else
   if ( ! -f "$CASEROOT/user_nl_clm" ) then
      cp $CODEROOT/lnd/clm/bld/user_nl_clm $CASEROOT/user_nl_clm
   endif
endif

# create clm.buildnml.csh
cat >! $CASEBUILD/clm.buildnml.csh << EOF
#! /bin/csh -f 

# Error check that CLM_USRDAT_NAME is set if CLM_USRDAT grid
if (\$LND_GRID == reg ) then
   if ( \$GRID == CLM_USRDAT ) then
      if ( ! \$?CLM_USRDAT_NAME ) then
         echo "clm.buildnml.csh ERROR: must set CLM_USRDAT_NAME xml variable if GRID is CLM_USRDAT"
         exit -1
      else if ( \$CLM_USRDAT_NAME == UNSET ) then
         echo "clm.buildnml.csh ERROR: must set CLM_USRDAT_NAME variable to a value other than UNSET "
         echo "clm.buildnml.csh ERROR: for cases where GRID is CLM_USRDAT"
         exit -1
      endif
   endif
endif

# ---- set build-namelist flags ----

if (\$RUN_TYPE == startup ) then
   if ($CLM_FORCE_COLDSTART == on) then
     set START_TYPE = "cold"
   else
     set START_TYPE = "default"
   endif
else
   if (\$RUN_TYPE == hybrid ) then
     set START_TYPE = "startup"
   else
     set START_TYPE = \$RUN_TYPE
   endif
endif

set RESOLUTION = \$LND_GRID
if (\$LND_GRID == reg ) then
   if ( \$GRID == CLM_USRDAT ) then
      set RESOLUTION = \$CLM_USRDAT_NAME
   else
      set RESOLUTION = \$GRID
   endif
endif

set usecase = " "
if (\$CLM_NML_USE_CASE != "UNSET") then
  set usecase = "-use_case \$CLM_NML_USE_CASE"
endif

set rtm = " "
if (\$LND_GRID == reg || \$PTS_MODE == TRUE ) then
   # Global River Transport model off for single point mode
   set rtm = "-rtm off"
endif

set ignore = "-ignore_ic_date"
if (\$RUN_STARTDATE =~ *-01-01* || \$RUN_STARTDATE =~ *-09-01*) then
  set ignore = "-ignore_ic_year"
endif

set mask = " "
if (\$OCN_GRID == "reg") then
   # Don't set mask in this case
else if (\$ATM_GRID != \$OCN_GRID) then
   set mask = "-mask $OCN_GRID"
endif

set clm_usr_name = ""
if ( "\$CLM_USRDAT_NAME" != "UNSET" ) then
    set clm_usr_name = "-clm_usr_name \$CLM_USRDAT_NAME"
endif

set glc_opts = ""
if ("\$COMP_GLC" != "sglc" )then
   set glc_opts = "-glc_grid \$GLC_GRID -glc_smb .\$GLC_SMB. "
endif

@ clm_dtime    = ( 3600 * 24 ) / \$LND_NCPL
set fatmlndfrc = " fatmlndfrc='\${LND_DOMAIN_PATH}/\${LND_DOMAIN_FILE}' "
set co2_type   = " co2_type='\$CLM_CO2_TYPE' "

cd \$CASEBUILD/clmconf || exit -1 
set default_lnd_in_filename = "lnd_in"

set inst_counter = 1
while (\$inst_counter <= \$NINST_LND)

if (\$NINST_LND > 1) then
   set inst_string = \$inst_counter
   if (\$inst_counter <= 999) set inst_string = "0\$inst_string"
   if (\$inst_counter <=  99) set inst_string = "0\$inst_string"
   if (\$inst_counter <=   9) set inst_string = "0\$inst_string"
   set inst_string = "_\${inst_string}"    
else
   set inst_string = ""       
endif
set lnd_in_filename = \${default_lnd_in_filename}\${inst_string}
set finidat = " "
if (\$RUN_TYPE == hybrid) then
   if (-e \${RUN_REFCASE}.clm2\${inst_string}.r.\${RUN_REFDATE}-\${RUN_REFTOD}.nc) then
     set  finidat = " finidat='\${RUN_REFCASE}.clm2\${inst_string}.r.\${RUN_REFDATE}-\${RUN_REFTOD}.nc' "
   else   
     set finidat = " finidat='\${RUN_REFCASE}.clm2.r.\${RUN_REFDATE}-\${RUN_REFTOD}.nc' "
   endif
endif
set nrevsn  = " "
if (\$RUN_TYPE == branch) then
  if (-e \${RUN_REFCASE}.clm2\${inst_string}.r.\${RUN_REFDATE}-\${RUN_REFTOD}.nc) then
    set nrevsn   = "  nrevsn='\${RUN_REFCASE}.clm2\${inst_string}.r.\${RUN_REFDATE}-\${RUN_REFTOD}.nc' "
  else 
    set nrevsn   = "  nrevsn='\${RUN_REFCASE}.clm2.r.\${RUN_REFDATE}-\${RUN_REFTOD}.nc' "
  endif
endif

cat >! \$CASEBUILD/clmconf/cesm_namelist << EOF1
&clm_inparm
 dtime = \$clm_dtime
 \$fatmlndfrc 
 \$co2_type
 \$finidat
 \$nrevsn
EOF1
if (-e \$CASEROOT/user_nl_clm\${inst_string}) then
  \$UTILROOT/Tools/user_nl_add -user_nl_file \$CASEROOT/user_nl_clm\${inst_string} >> \$CASEBUILD/clmconf/cesm_namelist 
endif
cat >> \$CASEBUILD/clmconf/cesm_namelist << EOF1
/
EOF1

if (-e \$CASEBUILD/clm.input_data_list) then
  rm \$CASEBUILD/clm.input_data_list
endif

\$CODEROOT/lnd/clm/bld/build-namelist -infile \$CASEBUILD/clmconf/cesm_namelist \
    -csmdata \$DIN_LOC_ROOT  \
    -inputdata \$CASEBUILD/clm.input_data_list \
    -namelist "&clm_inparm \$CLM_NAMELIST_OPTS /" \
     \$ignore \$usecase \$clm_usr_name \$glc_opts \
    -res \$RESOLUTION \$mask \
    -clm_start_type \$START_TYPE \
    -rtm_tstep 10800 \
    -co2_ppmv \$CCSM_CO2_PPMV \
    \$rtm  -glc_nec \$GLC_NEC \
    -config \$CASEBUILD/clmconf/config_cache.xml \$CLM_BLDNML_OPTS
    
if (-d \${RUNDIR}) then
  cp \$CASEBUILD/clmconf/lnd_in \${RUNDIR}/\$lnd_in_filename || exit -2
endif

@ inst_counter = \$inst_counter + 1

end

EOF

#--------------------------------------------------------------------
# Create clm.buildexe.csh
#--------------------------------------------------------------------

cat >! $CASEBUILD/clm.buildexe.csh << EOF2
#! /bin/csh -f 

cd \$OBJROOT/lnd/obj

setenv COMP "unknown"
if (\$COMP_INTERFACE == 'MCT' ) setenv COMP mct
if (\$COMP_INTERFACE == 'ESMF') setenv COMP esmf

\cat >! .tmp << EOF; cmp -s .tmp Filepath || mv -f .tmp Filepath 
EOF2

cat $CASEBUILD/clmconf/Filepath >> $CASEBUILD/clm.buildexe.csh

cat >> $CASEBUILD/clm.buildexe.csh << EOF2
EOF

set clmdefs = "`cat \$CASEBUILD/clmconf/CESM_cppdefs`"
if ( ! \$?GMAKE ) setenv GMAKE gmake
\$GMAKE complib -j \$GMAKE_J MODEL=clm COMPLIB=\$LIBROOT/liblnd.a USER_CPPDEFS="\$clmdefs" -f \$CASETOOLS/Makefile || exit 2

wait

EOF2

