#! /bin/csh -f
#--------------------------------------------------------------------
# Configure clm and create resolved namelist script for cpl7
#--------------------------------------------------------------------

if !(-d $CASEBUILD) mkdir $CASEBUILD
if !(-d $CASEBUILD/clmconf) mkdir $CASEBUILD/clmconf

#--------------------------------------------------------------------
# Error checks
#--------------------------------------------------------------------

#--------------------------------------------------------------------
# Invoke configure
#--------------------------------------------------------------------

set cfgdir = $CASEBUILD/clmconf  
set config = $cfgdir/config_cache.xml
cd $cfgdir

$CODEROOT/lnd/clm*/bld/configure -spmd -mode ext_ccsm_seq -rtm on -dust on -prog_seasalt on -clm_bld $cfgdir \
         -usr_src $CASEROOT/SourceMods/src.clm $CLM_CONFIG_OPTS || exit -1 

#--------------------------------------------------------------------
# Invoke build-namelist
#--------------------------------------------------------------------

# --- set hybrid run initial dataset 
set finidat = " "
if ($RUN_TYPE == hybrid) set finidat = " finidat = '${RUN_REFCASE}.clm2.r.${RUN_REFDATE}-00000.nc' "

# --- set branch run restart dataset 
set nrevsn = " "
if ($RUN_TYPE == branch) set nrevsn   = ${RUN_REFCASE}.clm2.r.${RUN_REFDATE}-00000.nc 

set config = $CASEBUILD/clmconf/config_cache.xml

set clm_demand = "$CLM_DEMAND,forganic,furbinp"

@ clm_dtime  = ( 3600 * 24 ) / $LND_NCPL
@ rtm_nsteps = ( 3600 * 3  ) / $clm_dtime  # rtm is done every 3 hours

cat >> $cfgdir/ccsm_namelist << EOF1
 &clm_inparm
 dtime            =  $clm_dtime
 rtm_nsteps       =  $rtm_nsteps
$finidat
 nrevsn           = '$nrevsn'
 hist_mfilt       =  1
 hist_nhtfrq      =  0
 co2_ppmv         =  $CCSM_CO2_PPMV
EOF1

if ($RUN_TYPE == startup && $CLM_ARB_IC != off) then
   set START_TYPE = "arb_ic"
else
   set START_TYPE = $RUN_TYPE
   if ($RUN_TYPE == hybrid ) set START_TYPE = "startup"
endif

if ($CLM_FORCE_COLDSTART == on) then
   set START_TYPE = "cold"
endif

if ($ATM_GRID == $OCN_GRID) then
  set mask = " "
else
  set mask = "-mask $OCN_GRID"
endif

if ($LND_GRID == pt1 ) then
   set RESOLUTION = $CLM_PT1_NAME
   set mask = " "
else
   set RESOLUTION = $LND_GRID
endif

# following logic may need tweaking to account for configurations where start year is important
if ($RUN_STARTDATE =~ *-01-01* || $RUN_STARTDATE =~ *-09-01*) then
    set ignore = "-ignore_ic_year"
else
    set ignore = "-ignore_ic_date"
endif

set options = "$CLM_BLDNML_OPTS -config $config -res $RESOLUTION $mask "
set options = "$options -clm_demand $clm_demand $ignore "
set options = "$options -infile ccsm_namelist -start_type $START_TYPE -case $CASE "
$CODEROOT/lnd/clm*/bld/build-namelist $options -namelist "&clm_inparm $CLM_NAMELIST_OPTS /" -csmdata \$DIN_LOC_ROOT -inputdata $CASEBUILD/clm.input_data_list || exit -1

# ---------------------------------------------------------------------------
#  Create namelist and prestage data script
# ---------------------------------------------------------------------------

cat >! $CASEBUILD/clm.buildnml.csh << EOF1
#! /bin/csh -f 

set exedir = \$RUNDIR; cd \$exedir

#******************************************************************#
#                  WARNING:                                        #
# - CLM namelist and CAM variable dtime must have the same value   #
# - If the user changes any input datasets - be sure to give it a  #
#   unique filename. Do not duplicate any existing input files     #
#******************************************************************#

cat >! lnd_in << EOF
EOF1
cat lnd_in >> $CASEBUILD/clm.buildnml.csh || exit -1
cat >> $CASEBUILD/clm.buildnml.csh << EOF1

EOF
EOF1

#--------------------------------------------------------------------
# Create clm.buildexe.csh
#--------------------------------------------------------------------

cat >! $CASEBUILD/clm.buildexe.csh << EOF2
#! /bin/csh -f 

set objdir = \$OBJROOT/lnd/obj; cd \$objdir

#--------------------------------------------------------------------
# check basic task and thread settings
#--------------------------------------------------------------------

cp -f \$CASEBUILD/clmconf/misc.h       .tmp
cmp -s .tmp misc.h       || mv -f .tmp misc.h
cp -f \$CASEBUILD/clmconf/preproc.h    .tmp
cmp -s .tmp preproc.h    || mv -f .tmp preproc.h
cp -f \$CASEBUILD/clmconf/CCSM_cppdefs .tmp
cmp -s .tmp CCSM_cppdefs || mv -f .tmp CCSM_cppdefs

\cat >! .tmp << EOF; cmp -s .tmp Filepath || mv -f .tmp Filepath
EOF2
cat $CASEBUILD/clmconf/Filepath >> $CASEBUILD/clm.buildexe.csh
cat >> $CASEBUILD/clm.buildexe.csh << EOF2
EOF

#
# Build the clm library
#
set clmdefs = "`cat \$CASEBUILD/clmconf/CCSM_cppdefs`"
gmake complib -j \$GMAKE_J MODEL=clm COMPLIB=\$LIBROOT/liblnd.a MACFILE=\$CASEROOT/Macros.\$MACH USER_CPPDEFS="\$clmdefs" -f \$CASEROOT/Tools/Makefile || exit 2

EOF2
