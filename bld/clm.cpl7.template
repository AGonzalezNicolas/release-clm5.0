#! /bin/csh -f
#--------------------------------------------------------------------
# Configure clm and create resolved namelist script for cpl7
#--------------------------------------------------------------------

if !(-d $CASEBUILD) mkdir $CASEBUILD
if !(-d $CASEBUILD/clmconf) mkdir $CASEBUILD/clmconf

#--------------------------------------------------------------------
# Error checks
#--------------------------------------------------------------------

#--- if CLM is expecting co2 from coupler, then must turn on the co2 interfaces ---   
if ( "$CLM_CO2_TYPE" != constant && "$CCSM_BGC" == none) then  
   echo "clm error: CLM_CO2_TYPE is not constant, must set CCSM_BGC to other than none"
   exit 3
endif

#--------------------------------------------------------------------
# Invoke configure
#--------------------------------------------------------------------

if ( $CLM_BGC == CN )then
  set bgc = '-bgc cn'
  set maxpft = '-maxpft numpft+1'
else if ( $CLM_BGC == CASA )then
  set bgc = '-bgc casa'
  set maxpft = '-maxpft 4'
else
  set bgc = '-bgc none'
  set maxpft = '-maxpft 4'
endif

set cfgdir = $CASEBUILD/clmconf  
set config = $cfgdir/config_cache.xml
cd $cfgdir

$CODEROOT/lnd/clm*/bld/configure -spmd -mode ext_ccsm_seq -rtm on $bgc -dust on -prog_seasalt on $maxpft -clm_bld $cfgdir \
         -usr_src $CASEROOT/SourceMods/src.clm || exit -1 

#--------------------------------------------------------------------
# Invoke build-namelist
#--------------------------------------------------------------------

# --- set hybrid run initial dataset 
set finidat = " "
if ($RUN_TYPE == hybrid) set finidat = " finidat = '${RUN_REFCASE}.clm2.r.${RUN_REFDATE}-00000.nc' "

# --- set branch run restart dataset 
set nrevsn = " "
if ($RUN_TYPE == branch) set nrevsn   = ${RUN_REFCASE}.clm2.r.${RUN_REFDATE}-00000.nc 

set sim_year = '-sim_year 2000'
if ($CCSM_IPCC == 1870_control       ) set sim_year = '-sim_year 1890'
if ($CCSM_IPCC == "1870-2000_control") set sim_year = ' '

set config = $CASEBUILD/clmconf/config_cache.xml

if ($CCSM_IPCC == "1870-2000_control") then
  setenv CLM_DYNNDEP "transient"
endif

set clm_demand = "$CLM_DEMAND,forganic"
if ($CLM_DYNNDEP == "transient" && $CLM_BGC == "CN" )then
   set clm_demand = "$clm_demand,fndepdyn"
endif
if ($CLM_DYNPFT  == "transient" )then
    set clm_demand = "$clm_demand,fpftdyn"
endif

@ clm_dtime  = ( 3600 * 24 ) / $LND_NCPL
@ rtm_nsteps = ( 3600 * 3  ) / $clm_dtime  # rtm is done every 3 hours

cat >> $cfgdir/ccsm_namelist << EOF1
 &clm_inparm
 dtime            =  $clm_dtime
 rtm_nsteps       =  $rtm_nsteps
$finidat
 nrevsn           = '$nrevsn'
 hist_mfilt       =  1
 hist_nhtfrq      =  0
 hist_crtinic     = 'NONE'
 co2_ppmv         =  $CCSM_CO2_PPMV
 co2_type         = '$CLM_CO2_TYPE'
EOF1

if ($RUN_TYPE == startup && $CLM_ARB_IC != off) then
   set START_TYPE = "arb_ic"
else
   set START_TYPE = $RUN_TYPE
   if ($RUN_TYPE == hybrid ) set START_TYPE = "startup"
endif

if ($ATM_GRID == $OCN_GRID) then
   set mask = USGS
   if ($ATM_GRID == 1.9x2.5 || $ATM_GRID == 0.9x1.25 || $ATM_GRID == 0.47x0.63 || $ATM_GRID == 0.23x0.31 ) \
      set mask = gx1v5
else
  set mask = $OCN_GRID
endif

if ($LND_GRID == pt1 ) then
   set RESOLUTION = $CLM_PT1_NAME
   set mask = navy
   if ($LND_GRID == 1x1_tropicAtl ) set mask = test
else
   set RESOLUTION = $LND_GRID
endif

set mask = "-mask $mask"

set options = "$CLM_BLDNML_OPTS -config $config -res $RESOLUTION $mask $sim_year -clm_demand $clm_demand "
set options = "$options -infile ccsm_namelist -start_type $START_TYPE -case $CASE "
$CODEROOT/lnd/clm*/bld/build-namelist $options -csmdata \$DIN_LOC_ROOT -inputdata $CASEBUILD/clm.input_data_list || exit -1

# ---------------------------------------------------------------------------
#  Create namelist and prestage data script
# ---------------------------------------------------------------------------

cat >! $CASEBUILD/clm.buildnml.csh << EOF1
#! /bin/csh -f 

set exedir = \$RUNDIR; cd \$exedir

#******************************************************************#
#                  WARNING:                                        #
# - CLM namelist and CAM variable dtime must have the same value   #
# - If the user changes any input datasets - be sure to give it a  #
#   unique filename. Do not duplicate any existing input files     #
#******************************************************************#

cat >! lnd_in << EOF
EOF1
cat lnd_in >> $CASEBUILD/clm.buildnml.csh || exit -1
cat >> $CASEBUILD/clm.buildnml.csh << EOF1

EOF
EOF1

#--------------------------------------------------------------------
# Create clm.buildexe.csh
#--------------------------------------------------------------------

cat >! $CASEBUILD/clm.buildexe.csh << EOF2
#! /bin/csh -f 

set objdir = \$OBJROOT/lnd/obj; cd \$objdir

#--------------------------------------------------------------------
# check basic task and thread settings
#--------------------------------------------------------------------

cp -f \$CASEBUILD/clmconf/misc.h       .tmp
cmp -s .tmp misc.h       || mv -f .tmp misc.h
cp -f \$CASEBUILD/clmconf/preproc.h    .tmp
cmp -s .tmp preproc.h    || mv -f .tmp preproc.h
cp -f \$CASEBUILD/clmconf/CCSM_cppdefs .tmp
cmp -s .tmp CCSM_cppdefs || mv -f .tmp CCSM_cppdefs

\cat >! .tmp << EOF; cmp -s .tmp Filepath || mv -f .tmp Filepath
EOF2
cat $CASEBUILD/clmconf/Filepath >> $CASEBUILD/clm.buildexe.csh
cat >> $CASEBUILD/clm.buildexe.csh << EOF2
EOF

#
# Build the clm library
#
set clmdefs = "`cat \$CASEBUILD/clmconf/CCSM_cppdefs`"
gmake complib -j \$GMAKE_J MODEL=clm COMPLIB=\$LIBROOT/liblnd.a MACFILE=\$CASEROOT/Macros.\$MACH USER_CPPDEFS="\$clmdefs" -f \$CASEROOT/Tools/Makefile || exit 2

EOF2
