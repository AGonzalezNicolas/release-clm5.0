#! /bin/csh -f

if !(-d $CASEROOT/Buildnml_Prestage) mkdir $CASEROOT/Buildnml_Prestage
if !(-d $CASEROOT/Buildnml_Prestage/clmconf) mkdir $CASEROOT/Buildnml_Prestage/clmconf
if !(-d $CASEROOT/Buildexe) mkdir $CASEROOT/Buildexe

#--------------------------------------------------------------------
# Configure clm and create resolved namelist script
#--------------------------------------------------------------------

#--- if CLM is expecting co2 from coupler, then must turn on the co2 interfaces ---   
if ( "$CLM_CO2_TYPE" != constant && "$CCSM_BGC" == none) then  
   echo "clm error: CLM_CO2_TYPE is not constant, must set CCSM_BGC to other than none"
   exit 3
endif

# --- set hybrid run initial dataset 
set datainit = " "
if ($RUN_TYPE == hybrid) set datainit = ${RUN_REFCASE}.clm2.r.${RUN_REFDATE}-00000.nc 

# --- set branch run restart dataset 
set nrevsn = " "
if ($RUN_TYPE == branch) set nrevsn   = ${RUN_REFCASE}.clm2.r.${RUN_REFDATE}-00000.nc 

set targetos=" "
#if ($OS == 'UNICOS') then
#   set targetos="-target_os unicosmp"
#endif

if ( $CLM_BGC == CN )then
  set bgc = '-bgc cn'
  set maxpft = '-maxpft numpft+1'
else if ( $CLM_BGC == CASA )then
  set bgc = '-bgc casa'
  set maxpft = '-maxpft 4'
else
  set bgc = '-bgc none'
  set maxpft = '-maxpft 4'
endif

set sim_year = '-sim_year 2000'
if ($CCSM_IPCC == 1870_control       ) set sim_year = '-sim_year 1890'
if ($CCSM_IPCC == 2000_control       ) set sim_year = '-sim_year 2000'
if ($CCSM_IPCC == OFF                ) set sim_year = '-sim_year 2000'
if ($CCSM_IPCC == "1870-2000_control") set sim_year = ' '


set config = $CASEROOT/Buildnml_Prestage/clmconf/config_cache.xml

if ($CCSM_IPCC == "1870-2000_control") then
  setenv CLM_DYNNDEP "transient"
endif

if (     $CLM_DYNNDEP == "transient" && $CLM_BGC == "CN" ) set clm_demand = "-clm_demand fndepdyn"
if (     $CLM_DYNPFT  == "transient" && $?clm_demand )then
    set clm_demand = "$clm_demand,fpftdyn"
else if ($CLM_DYNPFT  == "transient" )then
    set clm_demand = '-clm_demand fpftdyn'
else
    set clm_demand = " "
endif

set res = $LND_GRID

if ($ATM_GRID == $OCN_GRID) then
   set mask = USGS
   if ($ATM_GRID == 1.9x2.5 || $ATM_GRID == 0.9x1.25 || $ATM_GRID == 0.47x0.63 || $ATM_GRID == 0.23x0.31 ) \
      set mask = gx1v5
else
  set mask = $OCN_GRID
endif
set mask = "-mask $mask"

set cfgdir = $CASEROOT/Buildnml_Prestage/clmconf  
set config = $cfgdir/config_cache.xml
cd $cfgdir

cat >> $cfgdir/ccsm_namelist << EOF1
 &clm_inparm
 dtime            =  $CLM_DTIME
 rtm_nsteps       =  $RTM_NSTEPS
 nrevsn           = '$nrevsn'
 hist_mfilt       =  1
 hist_nhtfrq      =  0
 hist_crtinic     = 'NONE'
 co2_ppmv         =  $CCSM_CO2_PPMV
 co2_type         = '$CLM_CO2_TYPE'
EOF1

$CODEROOT/lnd/clm*/bld/configure -verbose -spmd -mode ext_ccsm_seq -rtm on $bgc $maxpft -clm_bld $cfgdir \
         $targetos -usr_src $CASEROOT/SourceMods/src.clm || exit -1 

if ($RUN_TYPE == startup && $CLM_ARB_IC != off) then
   set RUNTYPE = "arb_ic"
else
   set RUNTYPE = $RUN_TYPE
endif

set options = "-config $config -res $LND_GRID $mask $sim_year $clm_demand "
set options = "$options -infile ccsm_namelist -start_type $RUNTYPE -case $CASE "
$CODEROOT/lnd/clm*/bld/build-namelist $options -csmdata \$DIN_LOC_ROOT || exit -1

# ---------------------------------------------------------------------------
#  Create namelist and prestage data script
# ---------------------------------------------------------------------------

cat >! $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh << EOF1
#! /bin/csh -f 

set exedir = \$EXEROOT/ccsm_se; cd \$exedir

#******************************************************************#
# If the user changes any input datasets - be sure to give it a    #
# unique filename. Do not duplicate any existing input files       #
#******************************************************************#

#******************************************************************#
#                  WARNING:                                        #
# CLM namelist variable dtime must have same value as              #
# CAM namelist variable dtime                                      #
#******************************************************************#

EOF1

if ($RUN_TYPE == 'hybrid') then
cat >> $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh << EOF1
if ( "\$runtype" != continue) then
set datainit = $datainit
#\$UTILROOT/Tools/ccsm_getfile $RUN_REFCASE/lnd/init/\$datainit || exit 1 
endif
EOF1
endif

if ($RUN_TYPE == 'branch') then
cat >> $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh << EOF1
if ( "\$runtype" != 'continue') then
#\$UTILROOT/Tools/ccsm_getfile $RUN_REFCASE/lnd/rest/\$nrevsn || exit 1
endif
EOF1
endif

cat >> $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh << EOF1
cat >! lnd_in << EOF
EOF1
cat lnd_in >> $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh || exit -1
cat >> $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh << EOF1

EOF
EOF1

#--------------------------------------------------------------------
# write out executable generating directives to resolved file
#--------------------------------------------------------------------

cat >! $CASEROOT/Buildexe/clm.buildexe.csh << EOF2
#! /bin/csh -f 

set exedir = \$EXEROOT/ccsm_se
set objdir = \$OBJROOT/lnd/obj; cd \$objdir

#--------------------------------------------------------------------
# check basic task and thread settings
#--------------------------------------------------------------------

if !(\$NTASKS_LND > 0) then
  echo "NTASKS_LND must be > 0, please correct in env_pes and reconfigure"
  exit -1
endif
if !(\$NTHRDS_LND > 0) then
  echo "NTHRDS_LND must be > 0, please correct in env_pes and reconfigure"
  exit -1
endif

cp -f \$CASEROOT/Buildnml_Prestage/clmconf/misc.h       .tmp
cmp -s .tmp misc.h       || mv -f .tmp misc.h
cp -f \$CASEROOT/Buildnml_Prestage/clmconf/preproc.h    .tmp
cmp -s .tmp preproc.h    || mv -f .tmp preproc.h
cp -f \$CASEROOT/Buildnml_Prestage/clmconf/CCSM_cppdefs .tmp
cmp -s .tmp CCSM_cppdefs || mv -f .tmp CCSM_cppdefs

\cat >! .tmp << EOF; cmp -s .tmp Filepath || mv -f .tmp Filepath
EOF2
cat $CASEROOT/Buildnml_Prestage/clmconf/Filepath >> $CASEROOT/Buildexe/clm.buildexe.csh
cat >> $CASEROOT/Buildexe/clm.buildexe.csh << EOF2
EOF

# Create the executable
#----------------------

if (\$BLDTYPE == 'TRUE') then
   cp \$BLDROOT/mkDepends . || exit 2
   cp \$BLDROOT/mkSrcfiles . || exit 2
   set THREAD = FALSE ;  if (\$NTHRDS_LND > 1) set THREAD = TRUE
   set clmdefs = "`cat \$CASEROOT/Buildnml_Prestage/clmconf/CCSM_cppdefs`"
   gmake complib -j \$GMAKE_J CLM_CPPDEFS="\$clmdefs" MODEL=clm DEPINC=\$INCROOT COMPLIB=\$objdir/../libclm.a \
       VPFILE=Filepath THREAD=\$THREAD -f  \$BLDROOT/Makefile MACFILE=\$CASEROOT/Macros.\$MACH  || exit 2
   cp -p *comp_*.mod \$LIBROOT/include/
endif

if !(-f \$objdir/../libclm.a) then
  echo "ERROR: clm library not available, check SETBLD"
  exit -1
endif

EOF2
