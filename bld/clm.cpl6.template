#! /bin/csh -f

if !(-d $CASEROOT/Buildnml_Prestage) mkdir $CASEROOT/Buildnml_Prestage
if !(-d $CASEROOT/Buildnml_Prestage/clmconf) mkdir $CASEROOT/Buildnml_Prestage/clmconf
if !(-d $CASEROOT/Buildexe) mkdir $CASEROOT/Buildexe

# --- set hybrid run initial dataset 
set datainit = " "
if ($RUN_TYPE == hybrid) set datainit = ${RUN_REFCASE}.clm2.i.${RUN_REFDATE}-00000.nc 

# --- set branch run restart dataset 
set nrevsn = " "
if ($RUN_TYPE == branch) set nrevsn   = ${RUN_REFCASE}.clm2.r.${RUN_REFDATE}-00000.nc 

# --- set basedate_num - used for branch or hybrid runs 
set basedate_num = `echo $RUN_STARTDATE | sed -e 's/-//g'`  # remove "-"

#
# Configure clm
#--------------------------------------------------------------------
if ( $CLM_BGC == CN )then
  set bgc="cn"
  set pft=numpft+1
else if ( $CLM_BGC == CASA )then
  set bgc="casa"
  set pft=4
else
  set bgc="none"
  set pft=4
endif
if ($OS == 'UNICOS') then
   set targetos="-target_os unicosmp"
else
   set targetos=" "
endif

set cfgdir = $CASEROOT/Buildnml_Prestage/clmconf
cd $cfgdir
$CODEROOT/lnd/clm*/bld/configure -s -spmd -mode ext_ccsm_con -rtm on -bgc $bgc -maxpft $pft -clm_bld $cfgdir \
                                  $targetos -usr_src $CASEROOT/SourceMods/src.clm

# ---------------------------------------------------------------------------
#  Create resolved namelist and prestage data script
# ---------------------------------------------------------------------------

set config = $CASEROOT/Buildnml_Prestage/clmconf/config_cache.xml

set sim_year = 2000
if ($CCSM_IPCC == 1870_control       ) set sim_year=1890
if ($CCSM_IPCC == 2000_control       ) set sim_year=2000
if ($CCSM_IPCC == OFF                ) set sim_year=2000
if ($CCSM_IPCC == "1870-2000_control") set sim_year=9999

if ($CCSM_IPCC == "1870-2000_control") then
  setenv CLM_DYNNDEP "transient"
endif

#set options = "-config $config -justvalue -ccsm -silent "
set options = "-config $config -justvalue -silent -csmdata $DIN_LOC_ROOT"
set options = "$options -res $res -options mask=$OCN_GRID,sim_year=$sim_year"

if ($CLM_ARB_IC == off )then
   set finidat    = `$CODEROOT/lnd/clm*/bld/queryDefaultNamelist.pl $options -demand -var finidat`
   if ( $status != 0 ) exit 1
else
   set finidat    = `$CODEROOT/lnd/clm*/bld/queryDefaultNamelist.pl $options         -var finidat`
   if ( $status != 0   ) exit 1
   if ( $finidat == "" ) set finidat = " "
endif
set pftdat     = `$CODEROOT/lnd/clm*/bld/queryDefaultNamelist.pl    $options -demand -var fpftcon`
if ( $status != 0 ) exit 1

set rdirdat    = `$CODEROOT/lnd/clm*/bld/queryDefaultNamelist.pl    $options -demand -var frivinp_rtm`
if ( $status != 0 ) exit 1

set fsurdat    = `$CODEROOT/lnd/clm*/bld/queryDefaultNamelist.pl    $options -demand -var fsurdat`
if ( $status != 0 ) exit 1

set fatmgrid   = `$CODEROOT/lnd/clm*/bld/queryDefaultNamelist.pl    $options -demand -var fatmgrid`
if ( $status != 0 ) exit 1

set fatmlndfrc = `$CODEROOT/lnd/clm*/bld/queryDefaultNamelist.pl    $options -demand -var fatmlndfrc`
if ( $status != 0 ) exit 1

set fndepdyn   = " "
set fndepdat   = " "
if ($CLM_BGC == CN && $CLM_DYNNDEP == "transient") then
   set fndepdyn = `$CODEROOT/lnd/clm*/bld/queryDefaultNamelist.pl $options -demand -var fndepdyn`
   if ( $status != 0 ) exit 1
else if ($CLM_BGC == CN )then
   set fndepdat = `$CODEROOT/lnd/clm*/bld/queryDefaultNamelist.pl $options -demand -var fndepdat`
   if ( $status != 0 ) exit 1
endif

set fpftdyn    = " "
if ($CLM_DYNPFT  == "transient") then
   set fpftdyn = `$CODEROOT/lnd/clm*/bld/queryDefaultNamelist.pl  $options -demand -var fpftdyn`
   if ( $status != 0 ) exit 1
endif

#------------------------------ start build-namelist version ------------------------------------------------------
#
# Build-namelist version (requires at least branch_tags/bstrms_tags/bstrms1_clm3_5_11/ or later)
#
#cat >! lndinput << EOF
# &drv_in
# start_ymd        =  $basedate_num
# start_tod        =  0
# brnch_retain_casename = .false.
# ctitle           = '$CASE $CASESTR'  
# /
# &clm_inparm
# rtm_nsteps       =  $rtm_nsteps 
# dtime            =  $dtime 
# irad             =  $iradsw
# hist_nhtfrq      =  0
# hist_crtinic     = 'YEARLY'
# nrevsn           = '$nrevsn'
# finidat          = '$finidat'
# fsurdat          = '$fsurdat'
# fatmgrid         = '$fatmgrid'
# fatmlndfrc       = '$fatmlndfrc'
# fpftcon          = '$pftdat'
# fpftdyn          = '$fpftdyn'
# frivinp_rtm      = '$rdirdat'
# co2_type         = '$CLM_CO2_TYPE'
# co2_ppmv         =  $CCSM_CO2_PPMV
# fndepdat         = '$fndepdat'
# fndepdyn         = '$fndepdyn'
# /
#EOF
#if ( $OS == ESOS ) then
#  cat lndinput | sed 's#^ */ *\$# clump_pproc      = 1#' >! lndinput.tmp
#  echo ' /' >>! lndinput.tmp
#  /bin/mv -f lndinput.tmp lndinput
#endif
#
#if ($RUN_TYPE == startup && $finidat == " ") then
#   set RUNTYPE = "arb_ic"
#else
#   set RUNTYPE = $RUN_TYPE
#endif
#set bnflags="-case $CASE -start_type $RUNTYPE -config $config -mask $OCN_GRID"
#set bnflags="$bnflags -sim_year $sim_year -infile lndinput -runlength 9999d"
#$CODEROOT/lnd/clm*/bld/build-namelist $bnflags || exit -1
#------------------------------ end build-namelist version ------------------------------------------------------
 
cat >! $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh << EOF1
#! /bin/csh -f 

# ---------------------------------------------------------------------------
#  Create namelist and prestage data script
# ---------------------------------------------------------------------------

source \$CASEROOT/env_conf         || exit -1
source \$CASEROOT/env_run          || exit -1
source \$CASEROOT/env_mach.\$MACH  || exit -1
source \$CASEROOT/env_pes.\$MACH   || exit -1


# Prestage data
#--------------------------------------------------------------------

#******************************************************************#
# If the user changes any input datasets - be sure to give it a    #
# unique filename. Do not duplicate any existing input files       #
#******************************************************************#

#******************************************************************#
#                  WARNING:                                        #
# CAM namelist variable dtime must have same value as              #
# CLM namelist variable dtime                                      #
# CAM namelist variable iradsw must have same value as             #
# CLM namelist variable irad                                       #
#******************************************************************#

# started as a $RUN_TYPE run

set runtype = $RUN_TYPE
if (\$CONTINUE_RUN == 'TRUE') set runtype = 'continue'

set exedir = \$EXEROOT/lnd
cd \$exedir

set finidat    = "$finidat" 
set nrevsn     = "$nrevsn"
set pftdat     = "$pftdat"
set rdirdat    = "$rdirdat"
set fsurdat    = "$fsurdat"
set fatmgrid   = "$fatmgrid"
set fatmlndfrc = "$fatmlndfrc"
set fndepdyn   = "$fndepdyn"
set fndepdat   = "$fndepdat"
set fpftdyn    = "$fpftdyn"

EOF1

if ($RUN_TYPE == 'startup') then
cat >> $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh << EOF1
if (\$PRESTAGE_DATA == TRUE) then
if ( "\$finidat" != " " && "\$runtype" != continue) then
   \$UTILROOT/Tools/ccsm_getinput \$finidat || exit 1 
endif
endif
EOF1
endif

if ($RUN_TYPE == 'hybrid') then
cat >> $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh << EOF1
if ( "\$runtype" != continue) then
   set finidat = "$datainit"
   \$UTILROOT/Tools/ccsm_getfile $RUN_REFCASE/lnd/init/\$finidat || exit 1 
endif
EOF1
endif

if ($RUN_TYPE == 'branch') then
cat >> $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh << EOF1
if ( "\$runtype" != 'continue') then
  \$UTILROOT/Tools/ccsm_getfile $RUN_REFCASE/lnd/rest/\$nrevsn || exit 1
endif
EOF1
endif

cat >> $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh << EOF1
if (\$PRESTAGE_DATA == TRUE) then
  \$UTILROOT/Tools/ccsm_getinput \$pftdat      || exit 1
  \$UTILROOT/Tools/ccsm_getinput \$rdirdat     || exit 1
  \$UTILROOT/Tools/ccsm_getinput \$fsurdat     || exit 1
  \$UTILROOT/Tools/ccsm_getinput \$fatmgrid    || exit 1
  \$UTILROOT/Tools/ccsm_getinput \$fatmlndfrc  || exit 1
  if ( "\$fndepdyn" != " ") then
    \$UTILROOT/Tools/ccsm_getinput \$fndepdyn  || exit 1
  endif
  if ( "\$fndepdat" != " ") then
    \$UTILROOT/Tools/ccsm_getinput \$fndepdat  || exit 1
  endif
  if ( "\$fpftdyn" != " ") then
    \$UTILROOT/Tools/ccsm_getinput \$fpftdyn   || exit 1
  endif
endif

EOF1

#--- if CLM is expecting co2 from coupler, then must turn on the co2 interfaces ---   
if ( "$CLM_CO2_TYPE" != constant && "$CCSM_BGC" == none) then  
   echo "atm error: CLM_CO2_TYPE is not constant, must set CCSM_BGC to other than none"
   exit 3
endif

cat >> $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh << EOF1
# Create resolved namelist
#--------------------------------------------------------------------
if (\$runtype == 'startup' ) set nsrest = 0
if (\$runtype == 'hybrid'  ) set nsrest = 0
if (\$runtype == 'branch'  ) set nsrest = 3
if (\$runtype == 'continue') set nsrest = 1

cat >! lnd.stdin << EOF
 &clm_inparm
 caseid           = '\$CASE'
 ctitle           = '\$CASE \$CASESTR'
 brnch_retain_casename = .false.
 nsrest           =  \$nsrest
 start_ymd        =  $basedate_num
 start_tod        =  0
 nelapse          = -9999
 rtm_nsteps       =  $rtm_nsteps
 dtime            =  $dtime
 irad             =  $iradsw
 csm_doflxave     = .true.
 hist_nhtfrq      =  0
 hist_crtinic     = 'YEARLY'
 nrevsn           = '\$nrevsn'
 finidat          = '\$finidat'
 fsurdat          = '\$fsurdat'
 fatmgrid         = '\$fatmgrid'
 fatmlndfrc       = '\$fatmlndfrc'
 fpftcon          = '\$pftdat'
 fpftdyn          = '\$fpftdyn'
 frivinp_rtm      = '\$rdirdat'
 co2_type         = '$CLM_CO2_TYPE'
 co2_ppmv         =  $CCSM_CO2_PPMV
 fndepdat         = '\$fndepdat'
 fndepdyn         = '\$fndepdyn'
 /
EOF

if ( \$OS == ESOS ) then
  cat lnd.stdin | sed 's#^ */ *\$# clump_pproc      = 1#' >! lnd.stdin.tmp
  echo ' /' >>! lnd.stdin.tmp
  /bin/mv -f lnd.stdin.tmp lnd.stdin
endif

EOF1
#------------------------------ start build-namelist version ------------------------------------------------------
#cat $cfgdir/lnd.stdin >> $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh
#cat >> $CASEROOT/Buildnml_Prestage/clm.buildnml_prestage.csh << EOF1
#EOF
#------------------------------ end build-namelist version ------------------------------------------------------
 
#--------------------------------------------------------------------
# write out executable generating directives to resolved file
#--------------------------------------------------------------------

cat >! $CASEROOT/Buildexe/clm.buildexe.csh << EOF2
#! /bin/csh -f 

source \$CASEROOT/env_conf || exit -1
source \$CASEROOT/env_run  || exit -1
source \$CASEROOT/env_mach.\$MACH  || exit -1
source \$CASEROOT/env_pes.\$MACH   || exit -1

# Use namelist filename instead of redirecting from stdin
echo "Use NLFILE rather than stdin for CLM"

set exedir = \$EXEROOT/lnd
set objdir = \$OBJROOT/lnd/obj

cd \$objdir

#--------------------------------------------------------------------
# check basic task and thread settings
#--------------------------------------------------------------------

if !(\$NTASKS_LND > 0) then
  echo "NTASKS_LND must be > 0, please correct in env_pes and reconfigure"
  exit -1
endif
if !(\$NTHRDS_LND > 0) then
  echo "NTHRDS_LND must be > 0, please correct in env_pes and reconfigure"
  exit -1
endif

cp -f \$CASEROOT/Buildnml_Prestage/clmconf/misc.h       .tmp
cmp -s .tmp misc.h       || mv -f .tmp misc.h
cp -f \$CASEROOT/Buildnml_Prestage/clmconf/preproc.h    .tmp
cmp -s .tmp preproc.h    || mv -f .tmp preproc.h
cp -f \$CASEROOT/Buildnml_Prestage/clmconf/CCSM_cppdefs .tmp
cmp -s .tmp CCSM_cppdefs || mv -f .tmp CCSM_cppdefs

\cat >! .tmp << EOF; cmp -s .tmp Filepath || mv -f .tmp Filepath
EOF2
cat $CASEROOT/Buildnml_Prestage/clmconf/Filepath >> $CASEROOT/Buildexe/clm.buildexe.csh
cat >> $CASEROOT/Buildexe/clm.buildexe.csh << EOF2
EOF

# Create the executable
#----------------------

if (\$BLDTYPE == 'TRUE') then
if (\$OS == 'UNICOS') then
  if (\`uname\` == 'Linux') then
    # cross compiler needs to run dependency program locally
    module purge
    source \$CASEROOT/env_mach.\$MACH  || exit -1
  endif
endif

  cp \$BLDROOT/mkDepends . || exit 2
  cp \$BLDROOT/mkSrcfiles . || exit 2

  set THREAD = FALSE ;  if (\$NTHRDS_LND > 1) set THREAD = TRUE
  set clmdefs = "`cat \$CASEROOT/Buildnml_Prestage/clmconf/CCSM_cppdefs`"
  if (\$SINGLE_EXEC == 'TRUE') then
    gmake complib -j \$GMAKE_J CLM_CPPDEFS="\$clmdefs" VPFILE=Filepath MODEL=clm COMPLIB=\$exedir/libclm.a \
        THREAD=\$THREAD -f  \$BLDROOT/Makefile MACFILE=\$CASEROOT/Macros.\$MACH    || exit 2
  else
    gmake -j \$GMAKE_J CLM_CPPDEFS="\$clmdefs" VPFILE=Filepath MODEL=clm EXEC=\$exedir/clm \
        THREAD=\$THREAD -f  \$BLDROOT/Makefile MACFILE=\$CASEROOT/Macros.\$MACH    || exit 2
  endif
endif

wait

if (\$SINGLE_EXEC == 'TRUE') then
  if !(-f \$exedir/libclm.a) then
    echo "ERROR: clm library not available, check SETBLD"
    exit -1
  endif
else
  if !(-f \$exedir/clm) then
    echo "ERROR: clm executable not available, check SETBLD"
    exit -1
  endif
endif

EOF2
